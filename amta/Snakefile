#find out which mapped_reads not ended up in metagenome assembly (ma)
PAIRS, = glob_wildcards('../grist/outputs/mapping/{pair}.mapped.fq.gz')
# then PAIRS would be [ "ERRXYZ.x.genomeABC", "ERRXYZ.x.genomeDEF"]
print(PAIRS)

rule all:
    input:
        expand("{pair}.diff3.csv", pair=PAIRS),

# minimap align mapped_reads to the "ma"
rule minimap:
    input:
        ma= "../assembly/{metagenome}.megahit.fa.gz",
        mapped_reads="../grist/outputs/mapping/{metagenome}.x.{genome}.mapped.fq.gz",
        
    output:
        minimap= "{metagenome}.x.{genome}.reads.x.ma.bam",
        
    conda: "env-diff.yml"
    threads: 32
    shell: """
        minimap2 -ax sr -t 32 {input.ma} {input.mapped_reads} | samtools view -b -F 4 - | samtools sort - > {output.minimap}
    """

# .bam to .fq file
rule samtool:
    input:
        bamfile= "{metagenome}.x.{genome}.reads.x.ma.bam",
        
    output:
        samfile= "{metagenome}.x.{genome}.reads.x.ma.fq.gz",
        
    conda: "env-diff.yml"
    threads: 1
    shell: """
        samtools bam2fq {input.bamfile} | gzip -9c > {output.samfile}
    """    

# compare total mapped_reads vs. the ones used for assembly, identify diff_reads
rule diff3_output:
    input:
        mapped_reads="../grist/outputs/mapping/{metagenome}.x.{genome}.mapped.fq.gz",
        samfile= "{metagenome}.x.{genome}.reads.x.ma.fq.gz",
        
    output:
        diff= "{metagenome}.x.{genome}.diff3.fq.gz",
    
    conda: "env-diff.yml"    
    threads: 1
    shell: """
        ../subtract/output-diff-reads.py {input.mapped_reads} {input.samfile} -o {output.diff}
        gzip {output.diff}
    """

# minimap align diff_reads to reference genome
rule diff3_map:
    input:
        diff= "{metagenome}.x.{genome}.diff3.fq.gz",
        mapped_genome="../grist/outputs/genomes/{genome}_genomic.fna.gz",
        
    output:
        minimap= "{metagenome}.x.{genome}.diff3.reads.x.genome.bam",
        
    conda: "env-diff.yml"
    threads: 32
    shell: """
        minimap2 -ax sr -t {threads} {input.mapped_genome} {input.diff} | samtools view -b -F 4 - | samtools sort - > {output.minimap}
    """

rule sort:
    input:
        bamfile= "{filename}.bam",
    output:
        sortfile= "{filename}.sort.bam",

    conda: "env-diff.yml"
    threads: 1
    shell: """
        samtools sort {input.bamfile} -o {output.sortfile}
    """

rule index:
    input:
        sortfile= "{filename}.sort.bam",
    output:
        indexfile= "{filename}.sort.bam.bai",

    conda: "env-diff.yml"
    threads: 1
    shell: """
        samtools index {input.sortfile}
    """

# bam1: contain all reads mapped to a genome
# bam2: contain reads that did not mapped to the assembly
# diff_reads csv including coverage
rule diff3_csv:
    input:
        bam1= "../grist/outputs/mapping/{metagenome}.x.{genome}.depth.txt",
        bam2= "{metagenome}.x.{genome}.diff3.reads.x.genome.sort.bam",
        bai2= "{metagenome}.x.{genome}.diff3.reads.x.genome.sort.bam.bai",
    output:
        csvfile= "{metagenome}.x.{genome}.diff3.csv",
    conda: "env-diff.yml"
    threads: 1
    shell: """
    ../subtract/calc-coverage-diff-2.py {input.bam1} {input.bam2} -o {output.csvfile}
    """    

